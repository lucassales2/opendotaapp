// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.versions = [
            'android_gradle'   : '3.2.0-alpha03',
            'support_library'  : '27.0.2',
            'constraint_layout': '1.1.0-beta5',
            'arch'             : '1.1.0',
            'arch_paging'      : '1.0.0-alpha5',
            'arch_room'        : '1.1.0-alpha1',
            'core_ktx'         : '0.1',
            'kotlin'           : '1.2.21',
            'espresso'         : '3.0.1',
            'dagger'           : '2.14.1',
            'ktlint'           : '0.14.0',
            'leakcanary'       : '1.5.4',
            'rx'               : '2.1.8',
            'rxlint'           : '1.6',
            'firebase'         : '11.8.0',
            'epoxy'            : '2.9.0',
            'retrofit'         : '2.3.0',
            'glide'            : '4.5.0'
    ]
    ext.kotlin_version = '1.2.21'
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${versions.android_gradle}"

        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:${versions.kotlin}"

        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'

        classpath 'io.fabric.tools:gradle:1.25.1'

        classpath 'com.google.firebase:firebase-plugins:1.1.5'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}


plugins {
    id "com.diffplug.gradle.spotless" version "3.8.0"
    id 'com.github.ben-manes.versions' version "0.17.0"
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target "**/*.kt"
            ktlint(versions.ktlint)
            licenseHeaderFile project.rootProject.file('spotless/copyright.kt')
        }
    }
}

String propOrDef(String propertyName, String defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}